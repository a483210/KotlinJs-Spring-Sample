apply plugin: 'java'

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlinx-serialization'

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
}

dependencies {
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVersion"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'

    implementation "io.github.microutils:kotlin-logging:${kotlinLoggingVersion}"

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}"

    implementation project(':common')
}

/**
 * 资源处理
 */
processResources {
    def importJs = getLocalProperties('importJs')
    if (importJs.equals('true')) {
        def jsProject = project(':dashboard')

        def task = jsProject.tasks.getByName('browserProductionWebpack')
        from(task.destinationDirectory) {
            into 'static'
        }

        dependsOn(task)
    }
}